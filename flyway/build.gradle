plugins {
    id "org.flywaydb.flyway" version "6.4.4"
    id "com.github.ben-manes.versions" version "0.28.0"
    id "de.undercouch.download" version "4.0.4"
}

apply plugin: 'java'

configurations {
    jdbc
}

flyway {
    url = 'jdbc:postgresql://host.docker.internal:5433/sample'
    user = 'sample'
    password = 'weak-password'
    encoding = 'UTF-8'
    schemas = ['version', 'sample']
}

tasks.register( 'schemaSpy', JavaExec ) {
    description = 'Generates database documents.'
    group = 'documentation'
    ext.outputDir = file( "${buildDir}/${name}" )

    download {
        ext.schemaSpyFile = file( "${downloadTaskDir}/${name}/schemaspy.jar" )

        src( 'https://github.com/schemaspy/schemaspy/releases/download/v6.1.0/schemaspy-6.1.0.jar' )
        dest( schemaSpyFile )
        overwrite = false
    }

    File driverFile = configurations.jdbc.files.first()

    main = '-jar'
    args = [
            schemaSpyFile,
            '-t', 'pgsql11',
            '-dp', driverFile,
            '-host', 'host.docker.internal',
            '-port', '5433',
            '-db', 'sample',
            '-u', 'sample',
            '-p', 'weak-password',
            '-schemas', 'version,sample',
            '-o', outputDir,
            '-vizjs',
            '-norows',
            '-nopages',
            '-noimplied'
    ]

    doFirst {
        outputDir.deleteDir()
    }
}

dependencies {
    runtimeOnly('org.postgresql:postgresql:42.2.14')
    jdbc('org.postgresql:postgresql:42.2.14')
}

repositories {
    mavenCentral()
    jcenter()
}
